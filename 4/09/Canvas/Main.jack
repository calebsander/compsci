class Main {
	static char ENTER;

	function void main() {
		var Pixel pixel1, pixel2, topLeft, bottomRight;
		var LinkedList screenElements, screenElementsWithSelected;

		let ENTER = 128;

		let screenElements = null;
		do Screen.setColor(true);
		while (true) {
			let pixel1 = Main.selectPixel(screenElements, Pixel.new(0, 0));
			let screenElementsWithSelected = LinkedList.new(
				Drawable.newRectangle(pixel1, pixel1),
				screenElements
			);
			let pixel2 = Main.selectPixel(screenElementsWithSelected, Pixel.copy(pixel1));
			do Memory.deAlloc(screenElementsWithSelected);
			let topLeft = Pixel.new(
				Math.min(pixel1.getX(), pixel2.getX()),
				Math.min(pixel1.getY(), pixel2.getY())
			);
			let bottomRight = Pixel.new(
				Math.max(pixel1.getX(), pixel2.getX()),
				Math.max(pixel1.getY(), pixel2.getY())
			);
			do Memory.deAlloc(pixel1);
			do Memory.deAlloc(pixel2);
			let screenElements = LinkedList.new(
				Drawable.newRectangle(topLeft, bottomRight),
				screenElements
			);
			do Main.drawElements(screenElements);
		}
		return; //unreachable
	}
	function void waitForEnterReleased() {
		var char keyPressed;

		let keyPressed = ENTER;
		while (keyPressed = ENTER) {
			let keyPressed = Keyboard.keyPressed();
		}
		return;
	}
	function void drawElements(LinkedList screenElements) {
		var LinkedList currentElements;
		var Drawable currentElement;

		let currentElements = screenElements;
		while (~(currentElements = null)) {
			let currentElement = currentElements.getHead();
			do currentElement.draw();
			let currentElements = currentElements.getNext();
		}
		return;
	}
	function Pixel selectPixel(LinkedList screenElements, Pixel pixel) {
		var char LEFT, RIGHT, UP, DOWN;
		var char keyPressed;
		var boolean noMovement;

		let LEFT = 130;
		let RIGHT = 132;
		let UP = 131;
		let DOWN = 133;

		let keyPressed = 0;
		while (~(keyPressed = ENTER)) {
			let keyPressed = Keyboard.keyPressed();
			let noMovement = false;
			if (keyPressed = LEFT) { do pixel.moveLeft(); }
			else {
				if (keyPressed = RIGHT) { do pixel.moveRight(); }
				else {
					if (keyPressed = UP) { do pixel.moveUp(); }
					else {
						if (keyPressed = DOWN) { do pixel.moveDown(); }
						else { let noMovement = true; }
					}
				}
			}
			if (~noMovement) {
				do Screen.clearScreen();
				do Main.drawElements(screenElements);
				do pixel.draw();
			}
			do Sys.wait(5);
		}
		do Main.waitForEnterReleased();
		return pixel;
	}
}