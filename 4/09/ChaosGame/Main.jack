class Main {
	function void main() {
		var int SIDE_LENGTH, START_X, START_Y;
		var int PIXEL_COUNT;
		var Random random;
		var Array pixels;
		var int lastSelected, nextSelected;
		var Pixel currentPosition, selectedPixel;

		let SIDE_LENGTH = 255;
		let START_X = (512 - SIDE_LENGTH) / 2;
		let START_Y = 0;
		let PIXEL_COUNT = 4;

		let random = Random.new(Keyboard.readInt("Enter a number: "));
		do Screen.clearScreen();
		do Screen.setColor(true);

		let pixels = Array.new(PIXEL_COUNT);
		let pixels[0] = Pixel.new(START_X, START_Y);
		let pixels[1] = Pixel.new(START_X + SIDE_LENGTH, START_Y);
		let pixels[2] = Pixel.new(START_X + SIDE_LENGTH, START_Y + SIDE_LENGTH);
		let pixels[3] = Pixel.new(START_X, START_Y + SIDE_LENGTH);
		let lastSelected = -1;
		let currentPosition = Pixel.new(START_X + (SIDE_LENGTH / 2), START_Y + (SIDE_LENGTH / 2));

		while (true) {
			let nextSelected = lastSelected;
			while (nextSelected = lastSelected) {
				let nextSelected = Util.mod(random.nextInt(), PIXEL_COUNT);
			}

			let selectedPixel = pixels[nextSelected];
			do currentPosition.halfTowards(selectedPixel);
			do currentPosition.draw();
		}
		return;
	}
}